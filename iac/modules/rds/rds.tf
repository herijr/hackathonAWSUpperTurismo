resource "random_password" "password" {
  length           = 12
  special          = true
  override_special = "#"
}

resource "aws_db_instance" "rds" {
  allocated_storage      = 250
  storage_type           = "gp2"
  engine                 = "sqlserver-ex"
  engine_version         = "15.00.4236.7.v1"
  identifier             = "db-${var.project_name}"
  license_model          = "license-included"
  instance_class         = var.db_instance_type
  multi_az               = false
  username               = var.db_user
  password               = random_password.password.result
  port                   = var.db_port
  availability_zone      = element(data.aws_availability_zones.available.names, 1)
  skip_final_snapshot    = true
  db_subnet_group_name   = var.db_group_private
  vpc_security_group_ids = var.sg_rds
  tags = merge(
    var.tags,
    {
      Name = "db-${var.project_name}"
    }
  )
}

resource "aws_secretsmanager_secret" "secrets" {
  description             = "Used for storing sensitive data, generated by terraform"
  name                    = lower("/${var.project_name}/terraform-secrets")
  recovery_window_in_days = 0
  tags = merge(
    var.tags,
    {
      Name = "sm-${var.project_name}"
    }
  )
}

resource "aws_secretsmanager_secret_version" "rds" {
  secret_id = aws_secretsmanager_secret.secrets.id
  secret_string = jsonencode(
    {
      "AWS_REGION"   = var.aws_region,
      "OWNER"        = var.owner,
      "PROJECT_NAME" = var.project_name,
      "DB_DATABASE"  = var.db_name,
      "DB_HOST"      = aws_db_instance.rds.address,
      "DB_PORT"      = aws_db_instance.rds.port,
      "DB_USER"      = aws_db_instance.rds.username,
      "DB_PASSWORD"  = random_password.password.result
    }
  )
}
